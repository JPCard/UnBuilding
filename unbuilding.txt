title Un-building
author Team 2
homepage www.puzzlescript.net

debug
verbose_logging

========
OBJECTS
========
(environmental)
background
darkgray
00000
00000
00000
00000
00000

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Hole
black
..0..
.000.
00000
.000.
..0..

(crates)
Scrate
Orange Yellow
00000
0.1.0
01110
0.1.0
00000

LittleCrate
Orange Yellow
00000
0...0
0...0
0...0
00000

BigCrate
LightBlue
00000
0...0
0...0
0...0
00000

FusedCrate
lightgreen
00000
00000
00000
00000
00000

RigidCrate
red blue
00100
01110
11011
01110
00100

RigidToLittleCrate (aux crate for rigid -> little transition)
red blue
00100
01110
11011
01110
00100

LittleToRigidCrate (aux crate for little -> rigid transition)
orange
00000
0...0
0...0
0...0
00000

(targets)
LittleTarget
Orange
.....
.000.
.0.0.
.000.
.....

BigTarget
LightBlue
.....
.000.
.0.0.
.000.
.....

RigidTarget
red blue 
.....
.010.
.1.1.
.010.
.....

FusedTarget
lightgreen
.....
.000.
.0.0.
.000.
.....

(players)
Cplayer
Black Orange White Blue Gray yellow lightgray
.555.
.4442
21216
.333.
.3.3.

Hplayer
Black Orange White Blue Gray yellow lightbrown
.555.
.4440
21216
.333.
.3.3.

Splayer
Black Orange White Blue Gray yellow lightblue
.5556
.4446
21210
.333.
.3.3.

Aplayer
Black Orange White Blue Gray yellow lightgreen
.555.
.4446
21212
.333.
.3.3.

Jplayer
Black Orange White Blue Gray yellow black red
.5556
.4447
21216
.333.
.3.3.

(tools)
UnHammer
black lightbrown
.0000
00000
..1..
..1..
..1..

UnSaw
black lightblue
.111.
.11..
.111.
.01..
.00..

UnAcid
white lightgreen
0..00
0110.
0110.
0110.
0000.

UnCrowbar
black red
.0000
.00.0
.11..
.11..
000..

Coffee
lightgray lightbrown white
22222
11111
11111
.000.
.000.

=======
LEGEND
=======
(environmental)
. = Background
# = Wall
0 = Hole

(crates)
* = LittleCrate
B = BigCrate
F = FusedCrate
G = RigidCrate

(targets)
O = LittleTarget
I = BigTarget 
P = FusedTarget
R = RigidTarget

(occupied targets)
@ = LittleTarget and LittleCrate
T = BigTarget and BigCrate
U = FusedTarget and FusedCrate
D = RigidTarget and RigidCrate

(players)
1 = Cplayer
2 = Hplayer
3 = Splayer
4 = Aplayer
5 = Jplayer

(tools)
H = UnHammer
S = UnSaw
A = UnAcid
J = UnCrowbar
C = Coffee

(Define crate types)
Crate = LittleCrate or BigCrate or FusedCrate or RigidCrate or LittleToRigidCrate or RigidToLittleCrate

(Define Player types)
Player = Hplayer or Cplayer or Splayer or Aplayer or Jplayer

(Define tool types)
Tool = UnHammer or UnSaw or Coffee or UnAcid or UnCrowbar

(Define Target Types)
Target = LittleTarget or BigTarget or FusedTarget or RigidTarget


=======
SOUNDS
=======

Crate MOVE 36772507
sfx0 24230102 (UnHammer)
sfx1 69271902 (UnSaw)
sfx2 88628902 (UnAcid)
sfx3 529705 (Coffee)
sfx4 529705 (UnCrowbar)
sfx5 39467907 (Fill Hole)
sfx6 49722504 (Fuse Crates)
sfx7 39455102 (Rigid to LittleCrates)
sfx8 42963902 (LittleCrates to Rigid)

================
COLLISIONLAYERS
================

Background
Target
Tool
Player, Wall, Crate, Scrate, Hole

======
RULES
======

[ >  CPlayer | LittleCrate no RigidCrate ] -> [  >  CPlayer | > LittleCrate  ]
[ >  APlayer | FusedCrate ] -> [  >  APlayer | > FusedCrate  ]

(Transformation Rules
- Couldn't figure out how to make this more generic
- Keep the number of tools low to not explode the number of transform rules
)
[action Cplayer UnHammer] 	-> [Hplayer Coffee] sfx0
[action Cplayer UnSaw] 		-> [Splayer Coffee] sfx1
[action Cplayer UnAcid]     -> [Aplayer Coffee] sfx2
[action Cplayer UnCrowbar]  -> [Jplayer Coffee] sfx4

[action Hplayer Coffee] 	-> [Cplayer UnHammer] sfx3
[action Hplayer UnSaw] 		-> [Splayer UnHammer] sfx1
[action Hplayer UnAcid]   	-> [Aplayer UnHammer] sfx2
[action Hplayer UnCrowbar]  -> [Jplayer UnHammer] sfx4

[action Splayer Coffee] 	-> [Cplayer UnSaw] sfx3
[action Splayer UnHammer] 	-> [Hplayer UnSaw] sfx0
[action Splayer UnAcid] 	-> [Aplayer UnSaw] sfx2
[action Splayer UnCrowbar] 	-> [Jplayer UnSaw] sfx4

[action Aplayer Coffee] 	-> [Cplayer UnAcid] sfx3
[action Aplayer UnHammer] 	-> [Hplayer UnAcid] sfx0
[action Aplayer UnSaw] 		-> [Splayer UnAcid] sfx1
[action Aplayer UnCrowbar] 	-> [Jplayer UnAcid] sfx4

[action Jplayer Coffee] 	-> [Cplayer UnCrowbar] sfx3
[action Jplayer UnHammer] 	-> [Hplayer UnCrowbar] sfx0
[action Jplayer UnSaw] 		-> [Splayer UnCrowbar] sfx1
[action Jplayer UnAcid] 	-> [Splayer UnCrowbar] sfx2


(UnHammer player rules)
[action Hplayer | crate | ... |crate] -> [Hplayer |crate| ... | < crate ]


(UnSaw player rules
Works by swapping out the targeted crates with a new crate type that will attract other crates.  Then swaps the crate back at the end. 
	- This requires a set of rules for each crate type, and for horizontal/vertical positioning
    - The saw will pull any crate type toward the sawed crate
    
)

horizontal [action splayer | LittleCrate] -> [splayer | Scrate]
vertical [scrate | ... | Crate] -> [scrate | ... | < Crate]
[scrate] -> [LittleCrate]

vertical [action splayer | LittleCrate] -> [splayer | Scrate]
horizontal [scrate | ... | Crate] -> [scrate | ... | < Crate]
[scrate] -> [LittleCrate]

horizontal [action splayer | BigCrate] -> [splayer | Scrate]
vertical [scrate | ... | Crate] -> [scrate | ... | < Crate]
[scrate] -> [BigCrate]

vertical [action splayer | BigCrate] -> [splayer | Scrate]
horizontal [scrate | ... | Crate] -> [scrate | ... | < Crate]
[scrate] -> [BigCrate]

horizontal[action splayer | FusedCrate] -> [splayer | Scrate]
vertical[scrate | ... | Crate] -> [scrate | ... | < Crate]
[scrate] -> [FusedCrate]

vertical[action splayer | FusedCrate] -> [splayer | Scrate]
horizontal[scrate | ... | Crate] -> [scrate | ... | < Crate]
[scrate] -> [FusedCrate]


(UnAcid rules  - fuses crates together that can fill in holes)
[> Aplayer | Crate | Crate] -> [Aplayer |  | FusedCrate] sfx6


(Hole rules)
[> FusedCrate | Hole ] -> [|] sfx5


(UnCrowbar rules - makes a group of consecutive crates a single RigidCrate)

[action Jplayer | LittleCrate] -> [ Jplayer | LittleToRigidCrate] sfx8 (transform initial crate into rigid crate)
+[LittleToRigidCrate | LittleCrate] -> [LittleToRigidCrate | LittleToRigidCrate] (each crate transforms its neighbors)

[LittleToRigidCrate] -> [RigidCrate] (transforms all crates at once)

[action Jplayer | RigidCrate] -> [ Jplayer | RigidToLittleCrate] (transform initial crate into little crate)
+[RigidToLittleCrate | RigidCrate] -> [RigidToLittleCrate | RigidToLittleCrate] (each crate transforms its neighbors)

[RigidToLittleCrate] -> [LittleCrate] sfx7 (transforms all crates at once)



[ >  Jplayer | RigidCrate ] -> [  >  Jplayer | > RigidCrate  ] (RigidCrate movement)
+ rigid [ moving RigidCrate | RigidCrate ] -> [ moving RigidCrate | moving RigidCrate ] 


(Prevent crates from landing on top of tools)
[> crate | tool] -> [crate | tool]

WINCONDITIONS
==============

All LittleTarget on LittleCrate
All BigTarget on BigCrate
All FusedTarget on FusedCrate
All RigidTarget on RigidCrate
Some Cplayer

=======
LEVELS
=======

Message You are an un-construction worker. Tools may not work as you may expect.
Message You must put all the boxes on their same-colored targets, and finish your day with a cup of coffee in hand.

(UnCrowbar levels)

	(moving boxes stucked on walls)
(1)
#############
#...j.......#
#...........#
#.....1.....#
#...........#
#.....*.r...#
#...*...r...#
#....#*.r...#
#############

(2)
#############
#1..j...#*.r#
#...........#
#...........#
#.....*...r.#
#...........#
#...........#
#....#*.r...#
#############

(3)
#############
#1..j.......#
#.......*...#
#...........#
#..........##
#.......r..*#
#...........#
#....#*.r..r#
#############

(4)
#############
#1..j...#*.r#
#####......##
#####...o..##
#o.*#..*...##
#..........*#
#...........#
#.r.*#*.r..o#
#############


(5)
#############
#############
#...*..r#####
#j.###....o##
#......##*.o#
#.*.1..*#####
#.......#####
##*r#..r#####
#############


	(taking boxes out of walls)
(1)
#############
#1..j.......#
#...........#
#........r..#
#.....*.....#
#..r........#
#....*#.....#
#....##.....#
#############

(2)
#############
#1..j.#.....#
#.....#.r.r.#
#.*...#..o..#
#....###....#
#...........#
#....*#*....#
#....###....#
#############



(3)
#############
#1..j.......#
#...........#
#......*..###
#...o.....*##
#..r.r....###
#.*.o..*....#
#...........#
#############

(4)
#############
#1..j.......#
#...........#
#..r.......*#
#..r..*.....#
#..r........#
#.*.........#
#...........#
#############

(5)
#############
#1..j.......#
#.....*.....#
#.........#*#
#...r.r.....#
#..r.r......#
#.*.........#
#........*..#
#############





	(moving boxes stuck on walls and taking boxes out of walls)



(1)
#############
#1..jr#######
#...*.#######
#r.......**r#
#or......*###
###*.....####
####*...o####
#####*..o####
#############



(2)
#############
#1..j.....###
#.........*##
#..........##
#######....r#
#.......*####
#.o..*#..#*r#
#o..*##....r#
#############


(3)
#############
#############
#...*.or#####
#j.###.....o#
#...*..####.#
#o..1..*###.#
#..###....*.#
#.o....*#####
#############

	(escaping a box mess)
    
(1)

#############
#**1j**######
#******######
#..**..######
#......*...r#
#......######
#......######
###..########
#############

(2)

#############
#**1j**######
#******######
#**..**.*..r#
#......######
#.****.######
#......######
##....#######
#############

(3)

#############
##*1j*#o#...#
##.**.#*#***#
##*..*#..***#
##....###...#
##...*###...#
##..*......r#
##.*..###*.r#
#############


(Debugging level)
(#############
#1.........A#
#...*.......#
#.....*B....#
#...H...B...#
#...........#
#...........#
#...P.I.O...#
#############)

(Unhammer Tutorial)
###########
#1...*#...#
#....*#...#
#....*#O.*#
#....*#...#
#H...*#...#
###########

(UnSaw Tutorial)
###########
#...#*#...#
#.4.#.#.c.#
#...#o#...#
#....*#..*#
#.s.#o#...#
#...#.#...#
#....*....#
###########

(UnAcid Tutorial)
###########
#...*O....#
#.........#
#000000000#
#.........#
#...B*....#
#1.......A#
###########

(UnChain Tutorial)
#############
#...j.......#
#...........#
#1..........#
#.....*..R..#
#..*........#
#....#*..R..#
#....#......#
#############

(Debugging level)
(#############
#1.........A#
#...*.......#
#.....*B....#
#...H...B...#
#...........#
#...........#
#...P.I.O...#
#############)

(Soapy's first level)
####################
#.#.....#.#........#
#.#.....#.#........#
#.#.....#.#........#
#.*#..............b#
#.#######.##########
#.......#.#........#
#.......#i#........#
#.......###........#
#hs..1...b.........#
####################


(soapy's second level)
####################
#1.#.......a.......#
#..#.#.............#
#..#.#.............#
#.*#.#.*.*..b......#
#..#o#...*..b......#
#..#.#...*..b......#
#..#.#.............#
#..#.############00#
#...o....s........p#
####################

(soapy's third level)
####################
#1sh#a.............#
#...#*#.*..........#
#.....0.....000....#
#....0.....0...0...#
#....*0..b.0.o.0...#
#.*..0.....0...0...#
#....0......000....#
#...b0.............#
#....0.............#
####################

(Soapy's fourth level)
#####################
#........#*#........#
#.j......s..........#
#...................#
##.................##
#*....bb...........r#
##.................r#
#..................r#
#h.................##
#.a.................#
#...1....#*#........#
#####################

(Unhammer Levels)


#############(more of an extended tutorial than a puzzle)
#..bi.1.ib..#
#.####h####.#
#...........#
#############


(Unsaw Levels)

#########
#......b#
#......i#
#.*.....#
#.......#
#...i..b#
#.1.....#
#.so....#
#########

###########
#.#*#.#.#.#
#..o..o..*#
##.#.#.#.##
#...s.....#
#.#.#.#.#.#
#*.o..@...#
##.#.#.#.##
#........1#
###########

############
#b#..b...#b#
#.s..1.....#
#......i...#
#..ii..ii..#
#...i......#
#..bbbbbb..#
#..........#
############

#############
#...........#
#...........#
#..#######..#
#..#i.i..#..#
#..#.o...#..#
#..#..b*b#..#
#s.#i.bib#..#
#..#######..#
#1...*......#
#...........#
#############

###############
#*.....*..#o..#
#.*#...#..#.#.#
#*.#..##..#.#.#
#.*#.**...#*#.#
#@o#.######@#.#
#o@#....oo.*#.#
#@o#...######.#
#o@..1.s......#
#.............#
###############

####################
#..b#.....*b*b*b*b*#
#...#.#.#.##########
#...#.#.#o#........#
#...#i#i#o#........#
#...###o#i#........#
#..1.b#o#o#........#
#.....###i#........#
#......b###........#
#.s......b#........#
####################

(dpickler, saw hammer levels)
#############
#..*#....i.b#
#..##.......#
#s..#..o...*#
#1..#.......#
#h..#.......#
#...#.......#
#.#*#.......#
#############

###############
#.............#
#.1...........#
#..h...*...s..#
#...#######...#
#...#*.o.*#...#
#...#.*o*.#...#
#..*#oo.oo#*..#
#...#.*o*.#...#
#...#*.o.*#...#
#...#######...#
#..s...*...h..#
#.............#
#.............#
###############

####################(lockpicking, saw only)
#..b#.....*b*b*b*b*#
#...#.#.#.##########
#...#.#.#o#........#
#...#i#i#o#........#
#...###o#i#........#
#..1.b#o#o#........#
#.....###i#........#
#......b###........#
#.s......b#........#
####################


###################(locksmithing, hammer and saw)
#.................#
#........#bbbbbbbh#
#s###############s#
#.b#.....*.*......#
#..#.#.#o#........#
#..#i#o#.#.b...b..#
#..#o#i#i#...*..b.#
#..###o#o#.*...*..#
#.1.b#o#.#.....*..#
#...####i#.b..b...#
#.....b#i#........#
#.....####........#
#.......b#........#
#.......##........#
#........s........#
###################

(It's a mystery!) 
(Plenty of tools, ideally uses all of the blocks we put on targets in earlier levels)
(Has X targets)
###hsajc################hsajc#################hsajc###
#....................................................#
#..........ooooooo...................................#
#.........o.......o..................................#
#........o..oo.oo..o.................................#
#........o.o.oo.oo.o.................................#
#.......o..o.oo.oo..o................................#
#...o...o...oo.oo...o..o.............................#
#..o.oooo...........ooo.o............................#
#.o..........oo.....o....o...........................#
h..o.ooo.....oo....oooo.o............................h
s...o...o..........o...o......1......................s
a.......o.........o..................................a
j........o........o..................................j
c........o.......o...................................c
#.........o.....o....................................#
#.........o.....o..oooo.o..o.o..o.ooo....oo...oo..oo.#
#.........o.....o..o....o..o.oo.o.o..o...oo...oo..oo.#
#.........o....o...oooo.o..o.oo.o.o..o.oo..oo.oo..oo.#
#.........o...o....o....o..o.o.oo.o..o.oo..oo.oo..oo.#
#.........o..o.....o....o..o.o.oo.o..o.oo..oo...oo...#
#........o..o......o.....oo..o..o.ooo..oo..oo...oo...#
#.......oooo.........................................#
#....................................................#
###hsajc###############hsajc##################hsajc###